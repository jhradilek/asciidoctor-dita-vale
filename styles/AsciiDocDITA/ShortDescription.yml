# Suggest assigning the [role="_abstract"] attribute list to a paragraph to
# be used as short description in DITA. Applies to PROCEDURE, CONCEPT,
# REFERENCE content types, and files without a content type. Does not apply
# to SNIPPET or ASSEMBLY.
---
extends: script
message: 'Assign [role="\_abstract"] to the first paragraph in a concept, procedure, or reference module to use it as <shortdesc> in DITA.'
level: warning
link: https://github.com/jhradilek/asciidoctor-dita-vale/blob/main/README.md#warnings
scope: raw
script: |
  text              := import("text")
  matches           := []

  r_comment_line    := text.re_compile("^(//|//[^/].*)$")
  r_comment_block   := text.re_compile("^/{4,}\\s*$")
  r_excluded_type   := text.re_compile("^:_(?:mod-docs-content|content|module)-type:[ \\t]*(?i:snippet|assembly)[ \\t]*$")
  r_any_content_type := text.re_compile("^:_(?:mod-docs-content|content|module)-type:")
  r_abstract        := text.re_compile("^\\[role=(?:\"_abstract\"|'_abstract'|_abstract)\\]")
  r_title           := text.re_compile("^=[ \\t]+.+$")

  document          := text.split(text.trim_suffix(scope, "\n"), "\n")

  in_comment_block  := false
  is_excluded       := false
  has_abstract      := false
  title_pos         := false
  start             := 0
  end               := 0

  for line in document {
    start += end
    end    = len(line) + 1

    if r_comment_block.match(line) {
      delimiter := text.trim_space(line)
      if ! in_comment_block {
        in_comment_block = delimiter
      } else if in_comment_block == delimiter {
        in_comment_block = false
      }
      continue
    }
    if in_comment_block { continue }
    if r_comment_line.match(line) { continue }

    if r_excluded_type.match(line) {
      is_excluded = true
      continue
    }

    if r_title.match(line) && ! title_pos {
      title_pos = {begin: start, end: start + end - 1}
      continue
    }

    if r_abstract.match(line) {
      has_abstract = true
    }
  }

  if ! is_excluded && ! has_abstract && title_pos {
    matches = append(matches, title_pos)
  }
