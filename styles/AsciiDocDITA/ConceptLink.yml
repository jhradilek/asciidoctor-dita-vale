# Report links and cross references outside of Additional resources in concept modules.
---
extends: script
message: "Move all links and cross references to Additional resources."
level: warning
link: https://github.com/jhradilek/asciidoctor-dita-vale/blob/main/README.md#warnings
scope: raw
script: |
  text              := import("text")
  matches           := []

  r_add_resources   := text.re_compile("^(?:={2,}[ \\t]+|\\.{1,2})Additional resources[ \\t]*$")
  r_any_title       := text.re_compile("^(?:={2,}[ \\t]+|\\.{1,2})[^ \\t.].*$")
  r_comment_block   := text.re_compile("^/{4,}\\s*$")
  r_comment_line    := text.re_compile("^(//|//[^/].*)$")
  r_content_type    := text.re_compile("^:_(?:mod-docs-content|content|module)-type:[ \\t]+(?i:assembly|concept)")
  r_inline_link     := text.re_compile("(?:|link:|<)(?:|\\+\\+)(?:https?|file|ftp|irc)://[^ \\t\\[\\]]+(?:|\\+\\+)(?:|\\[.*?\\])>?")
  r_inline_xref     := text.re_compile("<<[A-Za-z0-9/.:{].*?>>")
  r_link_macro      := text.re_compile("(?:link|mailto):(?:|[^: \\t\\[][^: \\t\\[]*)\\[.*?\\]")
  r_xref_macro      := text.re_compile("xref:[A-Za-z0-9/.:{].*?\\[.*?\\]")

  document          := text.split(text.trim_suffix(scope, "\n"), "\n")

  is_relevant       := false
  in_comment_block  := false
  in_add_resources  := false
  start             := 0
  end               := 0

  for line in document {
    start += end
    end    = len(line) + 1

    if r_comment_block.match(line) {
      delimiter := text.trim_space(line)
      if ! in_comment_block {
        in_comment_block = delimiter
      } else if in_comment_block == delimiter {
        in_comment_block = false
      }
      continue
    }
    if in_comment_block { continue }
    if r_comment_line.match(line) { continue }

    if r_content_type.match(line) {
      is_relevant = true
      continue
    }

    if r_add_resources.match(line) {
      in_add_resources = true
      continue
    }

    if r_any_title.match(line) {
      in_add_resources = false
      continue
    }

    if in_add_resources { continue }

    for i, entry in r_link_macro.find(line, -1) {
      matches = append(matches, {begin: start + entry[0].begin, end: start + entry[0].end - 1})
    }
    for i, entry in r_inline_link.find(line, -1) {
      matches = append(matches, {begin: start + entry[0].begin, end: start + entry[0].end - 1})
    }
    for i, entry in r_xref_macro.find(line, -1) {
      matches = append(matches, {begin: start + entry[0].begin, end: start + entry[0].end - 1})
    }
    for i, entry in r_inline_xref.find(line, -1) {
      matches = append(matches, {begin: start + entry[0].begin, end: start + entry[0].end - 1})
    }
  }

  if ! is_relevant {
    matches = []
  }
